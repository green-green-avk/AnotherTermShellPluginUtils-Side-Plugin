plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.green-green-avk'
version = '1.1'

android {
    namespace 'green_green_avk.anothertermshellpluginutils_side_plugin'

    compileSdk 31

    defaultConfig {
        minSdk 14
        targetSdk 31
        versionCode 2
        versionName version
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError true
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact(sourcesJar)
                artifact(javadocJar)
            }
        }
    }
}
